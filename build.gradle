apply plugin: 'java'
apply plugin: 'maven-publish'
apply plugin: 'org.ajoberstar.github-pages'
apply plugin: 'signing'
apply plugin: 'idea'
apply plugin: 'com.github.johnrengelman.shadow'
apply plugin: 'io.codearte.nexus-staging'

import com.github.jengelman.gradle.plugins.shadow.tasks.ConfigureShadowRelocation

configurations.all {
    // check for updates every build for dependencies with: 'changing: true'
    resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
}

repositories {
    mavenLocal()
    // Before LaunchDarkly release artifacts get synced to Maven Central they are here along with snapshots:
    maven { url "https://oss.sonatype.org/content/groups/public/" }
    mavenCentral()
}

allprojects {
    group = 'com.launchdarkly'
    version = "${version}"
    sourceCompatibility = 1.7
    targetCompatibility = 1.7
}

ext {
    sdkBasePackage = "com.launchdarkly.client.redis"
    sdkBaseName = "launchdarkly-java-server-sdk-redis-store"
}

ext.libraries = [:]

// Add dependencies to "libraries.internal" that are not exposed in our public API. These
// will be embedded with shaded names in the uberjar.
libraries.internal = [
    // We're not currently shading any dependencies. The Redis client has to be a normal
    // public dependency because we expose Jedis types in our API.
]

// Add dependencies to "libraries.external" that are exposed in our public API, or that have
// global state that must be shared between the SDK and the caller. These are not embedded
// in the uberjar; the caller must provide them on the classpath at runtime.
libraries.external = [
    "com.launchdarkly:launchdarkly-java-server-sdk:4.6.4",
    "redis.clients:jedis:2.9.0",
    "org.slf4j:slf4j-api:1.7.21"
]

// Add dependencies to "libraries.test" that are used only in unit tests.
libraries.test = [
    "org.hamcrest:hamcrest-all:1.3",
    "junit:junit:4.12",
    "ch.qos.logback:logback-classic:1.1.7",
    "com.launchdarkly:launchdarkly-java-server-sdk:4.6.4:test" // our unit tests use helper classes from the SDK
]

dependencies {
    implementation libraries.internal
    compileClasspath libraries.external
    runtime libraries.internal
    testImplementation libraries.test, libraries.internal, libraries.external

    // Unlike what the name might suggest, the "shadow" configuration specifies dependencies that
    // should *not* be shaded by the Shadow plugin when we build our shaded jars.
    shadow libraries.external
}

buildscript {
    repositories {
        jcenter()
        mavenCentral()
        mavenLocal()
    }
    dependencies {
        classpath 'org.ajoberstar:gradle-git:1.5.0-rc.1'
        classpath 'com.github.jengelman.gradle.plugins:shadow:2.0.4'
        classpath "io.codearte.gradle.nexus:gradle-nexus-staging-plugin:0.8.0"
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

shadowJar {
    baseName = sdkBaseName
    
    // No classifier means that the shaded jar becomes the default artifact
    classifier = ''

    manifest {
        attributes("Implementation-Version": version)
    }
}

task relocateShadowJar(type: ConfigureShadowRelocation) {
    target = tasks.shadowJar
    prefix = "com.launchdarkly.shaded"
}

tasks.shadowJar.dependsOn tasks.relocateShadowJar

artifacts {
    archives sourcesJar, javadocJar, shadowJar
}

test {
    testLogging {
        events "passed", "skipped", "failed", "standardOut", "standardError"
        showStandardStreams = true
        exceptionFormat = 'full'
    }
}

idea {
    module {
        downloadJavadoc = true
        downloadSources = true
    }
}

nexusStaging {
    packageGroup = "com.launchdarkly"
}

def pomConfig = {
    name 'LaunchDarkly Java SDK Redis integration'
    packaging 'jar'
    url 'https://github.com/launchdarkly/java-server-sdk-redis'

    licenses {
        license {
            name 'The Apache License, Version 2.0'
            url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
        }
    }

    developers {
        developer {
            id 'jkodumal'
            name 'John Kodumal'
            email 'john@launchdarkly.com'
        }
    }

    scm {
        connection 'scm:git:git://github.com/launchdarkly/java-server-sdk-redis.git'
        developerConnection 'scm:git:ssh:git@github.com:launchdarkly/java-server-sdk-redis.git'
        url 'https://github.com/launchdarkly/java-server-sdk-redis'
    }
}

publishing {
    publications {
        shadow(MavenPublication) { publication ->
            project.shadow.component(publication)

            artifactId = sdkBaseName
            artifact sourcesJar
            artifact javadocJar
            
            pom.withXml {
                def root = asNode()
                root.appendNode('description', 'Redis-backed feature store for the LaunchDarkly Java SDK')
                asNode().children().last() + pomConfig
            }
        }
    }
    repositories {
        mavenLocal()
        maven {
            def releasesRepoUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
            def snapshotsRepoUrl = "https://oss.sonatype.org/content/repositories/snapshots/"
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
            credentials {
                username ossrhUsername
                password ossrhPassword
            }
        }
    }
}

signing {
    sign publishing.publications.shadow
}
